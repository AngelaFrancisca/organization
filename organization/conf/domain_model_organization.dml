/** package module.organization.domain; **/

/*
 * @(#)domain_model_organization.dml
 *
 * Copyright 2009 Instituto Superior Tecnico, Jo√£o Figueiredo, Luis Cruz, Paulo Abrantes, Susana Fernandes
 * 
 *      https://fenix-ashes.ist.utl.pt/
 * 
 *   This file is part of the Organization Module for the MyOrg web application.
 *
 *   The Organization Module is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU Lesser General Public License as published
 *   by the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.*
 *
 *   The Organization Module is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *   GNU Lesser General Public License for more details.
 *
 *   You should have received a copy of the GNU Lesser General Public License
 *   along with the Organization Module. If not, see <http://www.gnu.org/licenses/>.
 * 
 */


valueType org.joda.time.DateTime as DateTime;
valueType org.joda.time.LocalDate as LocalDate;

valueType pt.utl.ist.fenix.tools.util.i18n.MultiLanguageString as MultiLanguageString;

enum module.organization.domain.person.IDDocumentType as IDDocumentType;
enum module.organization.domain.person.Gender as Gender;
enum module.organization.domain.person.MaritalStatus as MaritalStatus;

class module.organization.domain.AccountabilityType {
	String type;
	MultiLanguageString name;
}

class module.organization.domain.Accountability {
	LocalDate beginDate;
	LocalDate endDate;
}

class module.organization.domain.PartyType {
	String type;
	MultiLanguageString name;
}

class module.organization.domain.Party {
	String ojbConcreteClass;
	MultiLanguageString partyName;
}

	class module.organization.domain.Person extends module.organization.domain.Party {
		String documentIdNumber;
		IDDocumentType idDocumentType;
		LocalDate emissionDateOfDocumentId;
		LocalDate expirationDateOfDocumentId;
		String emissionLocationOfDocumentId;

		String fiscalCode;
				
		Gender gender;
		MaritalStatus maritalStatus;

		LocalDate birthDate;
		String birthDistrict;
		String birthDistrictSubdivision;
		String birthParish;
		
		String fathersName;
		String mothersName;
		
		// Add to relation
    	//private DomainReference<Country> nationality;
    	//private DomainReference<Country> countryOfBirth;
	}

	class module.organization.domain.Unit extends module.organization.domain.Party {
		String acronym;
	}

class module.organization.domain.ConnectionRule {
	String ojbConcreteClass;
}

	class module.organization.domain.PartyTypeConnectionRule extends module.organization.domain.ConnectionRule {
	}
	
	class module.organization.domain.UniqueNameAndAcronymConnectionRule extends module.organization.domain.ConnectionRule {
	}

/** Relations **/

relation UserPerson {
	myorg.domain.User playsRole user;
	module.organization.domain.Person playsRole person;
}

relation MyOrgAccountabilityType {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.AccountabilityType playsRole accountabilityTypes {
		multiplicity *;
	}
}

relation MyOrgAccountability {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.Accountability playsRole accountabilities {
		multiplicity *;
	}
}

relation MyOrgPartyType {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.PartyType playsRole partyTypes {
		multiplicity *;
	}
}

relation AccountabilityAccountabilityType {
	module.organization.domain.Accountability playsRole accountabilities {
		multiplicity *;
	}
	module.organization.domain.AccountabilityType playsRole accountabilityType;
}

relation MyOrgParty {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.Party playsRole parties {
		multiplicity *;
	}
}

relation MyOrgTopUnit {
	myorg.domain.MyOrg playsRole myOrgFromTopUnit;
	module.organization.domain.Unit playsRole topUnits {
		multiplicity *;
	}
}

relation PartyTypeParty {
	module.organization.domain.PartyType playsRole partyTypes {
		multiplicity *;
	}
	module.organization.domain.Party playsRole parties {
		multiplicity *;
	}
}

relation PartyParentAccountabilities {
	module.organization.domain.Party playsRole child;
	module.organization.domain.Accountability playsRole parentAccountabilities {
		multiplicity *;
	}
}

relation PartyChildAccountabilities {
	module.organization.domain.Party playsRole parent;
	module.organization.domain.Accountability playsRole childAccountabilities {
		multiplicity *;
	}
}

relation MyOrgConnectionRule {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.ConnectionRule playsRole connectionRules {
		multiplicity *;
	}
}

relation ConnectionRuleAccountabilityType {
	module.organization.domain.ConnectionRule playsRole connectionRules {
		multiplicity *;
	}
	module.organization.domain.AccountabilityType playsRole accountabilityTypes {
		multiplicity *;
	}
}

relation PartyTypeConnectionRuleParentPartyType {
	module.organization.domain.PartyType playsRole allowedParent;
	module.organization.domain.PartyTypeConnectionRule playsRole childConnectionRules {
		multiplicity *;
	}
}

relation PartyTypeConnectionRuleChildPartyType {
	module.organization.domain.PartyType playsRole allowedChild;
	module.organization.domain.PartyTypeConnectionRule playsRole parentConnectionRules {
		multiplicity *;
	}
}
