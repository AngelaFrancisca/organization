/** package module.organization.domain; **/

/*
 * @(#)domain_model_organization.dml
 *
 * Copyright 2009 Instituto Superior Tecnico
 * Founding Authors: Jo√£o Figueiredo, Luis Cruz, Paulo Abrantes, Susana Fernandes
 * 
 *      https://fenix-ashes.ist.utl.pt/
 * 
 *   This file is part of the Organization Module for the MyOrg web application.
 *
 *   The Organization Module is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU Lesser General Public License as published
 *   by the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.*
 *
 *   The Organization Module is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *   GNU Lesser General Public License for more details.
 *
 *   You should have received a copy of the GNU Lesser General Public License
 *   along with the Organization Module. If not, see <http://www.gnu.org/licenses/>.
 * 
 */


valueType pt.utl.ist.fenix.tools.util.i18n.MultiLanguageString as MultiLanguageString {
    externalizeWith {
        String exportAsString();
    }

    internalizeWith importFromString();
}


class module.organization.domain.AccountabilityType {
	String type;
	MultiLanguageString name;
}

class module.organization.domain.Accountability {
	LocalDate beginDate;
	LocalDate endDate;
}

class module.organization.domain.PartyType {
	String type;
	MultiLanguageString name;
}

class module.organization.domain.Party {
	String ojbConcreteClass;
	MultiLanguageString partyName;
}

	class module.organization.domain.Person extends module.organization.domain.Party {
	}

	class module.organization.domain.Unit extends module.organization.domain.Party {
		String acronym;
	}

class module.organization.domain.ConnectionRule {
	String ojbConcreteClass;
}

	class module.organization.domain.connectionRules.PartyTypeConnectionRule extends module.organization.domain.ConnectionRule {
	}
	
	class module.organization.domain.connectionRules.UniqueNameAndAcronymConnectionRule extends module.organization.domain.ConnectionRule {
	}

class module.organization.domain.OrganizationalModel {
	MultiLanguageString name;
}


/** Relations **/

relation UserPerson {
	myorg.domain.User playsRole user;
	module.organization.domain.Person playsRole person;
}

relation MyOrgAccountabilityType {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.AccountabilityType playsRole accountabilityTypes {
		multiplicity *;
	}
}

relation MyOrgAccountability {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.Accountability playsRole accountabilities {
		multiplicity *;
	}
}

relation MyOrgPartyType {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.PartyType playsRole partyTypes {
		multiplicity *;
	}
}

relation AccountabilityAccountabilityType {
	module.organization.domain.Accountability playsRole accountabilities {
		multiplicity *;
	}
	module.organization.domain.AccountabilityType playsRole accountabilityType;
}

relation MyOrgParty {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.Party playsRole parties {
		multiplicity *;
	}
}

relation MyOrgTopUnit {
	myorg.domain.MyOrg playsRole myOrgFromTopUnit;
	module.organization.domain.Unit playsRole topUnits {
		multiplicity *;
	}
}

relation MyOrgWithPersons {
	myorg.domain.MyOrg playsRole myOrgFromPerson;
	module.organization.domain.Person playsRole persons {
		multiplicity *;
	}
}

relation PartyTypeParty {
	module.organization.domain.PartyType playsRole partyTypes {
		multiplicity *;
	}
	module.organization.domain.Party playsRole parties {
		multiplicity *;
	}
}

relation PartyParentAccountabilities {
	module.organization.domain.Party playsRole child;
	module.organization.domain.Accountability playsRole parentAccountabilities {
		multiplicity *;
	}
}

relation PartyChildAccountabilities {
	module.organization.domain.Party playsRole parent;
	module.organization.domain.Accountability playsRole childAccountabilities {
		multiplicity *;
	}
}

relation MyOrgConnectionRule {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.ConnectionRule playsRole connectionRules {
		multiplicity *;
	}
}

relation ConnectionRuleAccountabilityType {
	module.organization.domain.ConnectionRule playsRole connectionRules {
		multiplicity *;
	}
	module.organization.domain.AccountabilityType playsRole accountabilityTypes {
		multiplicity *;
	}
}

relation PartyTypeConnectionRuleParentPartyType {
	module.organization.domain.PartyType playsRole allowedParent;
	module.organization.domain.connectionRules.PartyTypeConnectionRule playsRole childConnectionRules {
		multiplicity *;
	}
}

relation PartyTypeConnectionRuleChildPartyType {
	module.organization.domain.PartyType playsRole allowedChild;
	module.organization.domain.connectionRules.PartyTypeConnectionRule playsRole parentConnectionRules {
		multiplicity *;
	}
}

relation MyOrgOrganizationalModel {
	myorg.domain.MyOrg playsRole myOrg;
	module.organization.domain.OrganizationalModel playsRole organizationalModels {
		multiplicity *;
	}
}

relation OrganizationalModelParty {
	module.organization.domain.OrganizationalModel playsRole organizationalModels {
		multiplicity *;
	}
	module.organization.domain.Party playsRole parties {
		multiplicity *;
	}
}

relation OrganizationalModelAccountabilityType {
	module.organization.domain.OrganizationalModel playsRole organizationalModels {
		multiplicity *;
	}
	module.organization.domain.AccountabilityType playsRole accountabilityTypes {
		multiplicity *;
	}
}

